apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.pune.dance.fitness"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "v0.8.8-${getGitHash()}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    //setting apk name
    applicationVariants.all { variant ->
        variant.outputs.all {
            //changes the generated .apk name
            if (variant.name.contains("release")) {
                def formattedDate = new Date().format('dd.MM.yy_HHmm')
                outputFileName = "PDFitness_${variant.name}_${formattedDate}_${variant.versionName}.apk"
            }
        }
    }

    dataBinding {
        enabled = true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "admob_app_id", "ca-app-pub-3940256099942544~3347511713"
            resValue "string", "watch_online_session_ad_unit_id", "ca-app-pub-3940256099942544/5224354917"
            buildConfigField("String", "BASE_URL", '"debug/9hzDw5sP5UTQbXfVWbiR"')
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "admob_app_id", "ca-app-pub-4200151682628675~5184025564"
            resValue "string", "watch_online_session_ad_unit_id", "ca-app-pub-4200151682628675/3862849810"
            buildConfigField("String", "BASE_URL", '"production/jj4S8ZL4CVyPw53ZTXxq"')
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    //realm name helper
    kapt 'dk.ilios:realmfieldnameshelper:1.1.1'
    //RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
    //RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    //google analytics
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    //firebase authentication
    implementation 'com.google.firebase:firebase-auth:19.2.0'
    //firebase firestore
    implementation 'com.google.firebase:firebase-firestore:21.4.0'
    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    // LiveData
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    //LiveData streams
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.2.0'
    //material library
    implementation 'com.google.android.material:material:1.1.0-rc02'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    //admob
    implementation 'com.google.android.gms:play-services-ads:19.0.1'
    //Firebase Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    //Lottie files
    implementation 'com.airbnb.android:lottie:3.3.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
